ext.PROJECT_NAME = 'Gradle Plugin for Android projects'
description = 'Gradle Plugin for Android projects'
ext.ARTIFACT_ID = 'jdroid-gradle-android-plugin'

apply plugin: 'com.jdroid.gradle.plugin'
apply plugin: 'com.gradle.plugin-publish'

buildscript {
	dependencies {
		classpath "com.gradle.publish:plugin-publish-plugin:" + GRADLE_PLUGIN_PUBLISH_PLUGIN
		classpath 'com.jdroidtools:jdroid-gradle-project-plugin:' + JDROID_GRADLE_PLUGIN_VERSION
	}
}

repositories {
	maven { url 'https://maven.fabric.io/public' }
}

dependencies {

	implementation project(path: ":jdroid-gradle-java-plugin")

	// https://developer.android.com/tools/revisions/gradle-plugin.html
	// https://dl.google.com/dl/android/maven2/index.html
	implementation 'com.android.tools.build:gradle:3.1.2'

	// https://firebase.google.com/docs/perf-mon/get-started-android#add-performance-monitoringto-your-app
	// https://jcenter.bintray.com/com/google/firebase/firebase-plugins/
	implementation 'com.google.firebase:firebase-plugins:1.1.5'

	// https://developers.google.com/android/guides/google-services-plugin
	// https://jcenter.bintray.com/com/google/gms/google-services/
	implementation 'com.google.gms:google-services:4.0.1'

	// https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
	// https://maven.fabric.io/public/io/fabric/tools/gradle/maven-metadata.xml
	implementation 'io.fabric.tools:gradle:1.25.3'

	testImplementation 'junit:junit:4.12'
}

gradlePlugin {
	plugins {
		androidApplicationPlugin {
			id = 'com.jdroid.android.application'
			implementationClass = 'com.jdroid.gradle.android.AndroidApplicationGradlePlugin'
		}
		androidLibraryPlugin {
			id = 'com.jdroid.android.library'
			implementationClass = 'com.jdroid.gradle.android.AndroidLibraryGradlePlugin'
		}
	}
}

// Configuration to publish your plugin to the plugin portal
pluginBundle {
	website = 'http://www.jdroidtools.com/'
	vcsUrl = 'https://github.com/maxirosson/jdroid-gradle-plugin'

	plugins {
		androidAppPlugin {
			id = 'com.jdroid.android.application'
			displayName = 'Jdroid Android App Gradle Plugin'
			description = 'Gradle Plugin for Android apps'
			tags = ['jdroid', 'java', 'android']
		}
		androidLibPlugin {
			id = 'com.jdroid.android.library'
			displayName = 'Jdroid Android Library Gradle Plugin'
			description = 'Gradle Plugin for Android libraries'
			tags = ['jdroid', 'java', 'android']
		}
	}

	mavenCoordinates {
		groupId = "com.jdroidtools"
		artifactId = "jdroid-gradle-android-plugin"
	}
}
